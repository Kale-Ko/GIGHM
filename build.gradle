plugins {
    id "java"
    id "maven-publish"
}

sourceCompatibility = JavaVersion.toVersion(project.java_version)
targetCompatibility = JavaVersion.toVersion(project.java_version)

archivesBaseName = "gighm"
group = "io.github.kale_ko"
version = project.project_version

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:${project.lwjgl_version}")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-glfw"

    project.lwjgl_natives.split(",").each {
        implementation "org.lwjgl:lwjgl::${it}"
        implementation "org.lwjgl:lwjgl-opengl::${it}"
        implementation "org.lwjgl:lwjgl-glfw::${it}"
    }

    implementation "org.joml:joml:${project.joml_version}"
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    options.encoding = "UTF-8"
}

jar {
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    manifest {
        attributes "Name": "io/github/kale_ko/gighm/",
            "Specification-Title": "GIGHM",
            "Specification-Version": project.version,
            "Specification-Vendor": "Kale-Ko",
            "Implementation-Title": "io.github.kale_ko.gighm",
            "Implementation-Version": project.version,
            "Implementation-Vendor": "Kale-Ko",
            "Sealed": true
    }
}

tasks.register("fatJar", Jar) {
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    archiveClassifier = "fat"
    manifest {
        attributes "Main-Class": "io.github.kale_ko.gighm.tests.ExampleScene",
            "Name": "io/github/kale_ko/gighm/",
            "Specification-Title": "GIGHM",
            "Specification-Version": project.version,
            "Specification-Vendor": "Kale-Ko",
            "Implementation-Title": "io.github.kale_ko.gighm",
            "Implementation-Version": project.version,
            "Implementation-Vendor": "Kale-Ko",
            "Sealed": true
    }

    duplicatesStrategy = "exclude"
    from(sourceSets.main.output)
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? null : zipTree(it) })
}

build {
    finalizedBy tasks.fatJar
}

javadoc {
    title = project.name + " " + project.version

    doFirst {
        mkdir "./build/"
        mkdir "./build/docs/"
        file("./build/docs/overview.html").text = "<html><body><p class=\"overview\">" + project.project_description + "</p></body></html>"
    }

    options {
        overview "build/docs/overview.html"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHub-Packages"
            url = "https://maven.pkg.github.com/Kale-Ko/GIGHM"

            credentials {
                username = System.getenv("GH_PACKAGES_USER")
                password = System.getenv("GH_PACKAGES_TOKEN")
            }
        }
    }

    publications {
        ghp(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact tasks.jar
            artifact tasks.fatJar
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom.withXml {
                def node = asNode()

                def packagingNode = node.appendNode("packaging", "jar")

                def licenseNode = node.appendNode("licenses").appendNode("license")
                licenseNode.appendNode("name", "MIT")
                licenseNode.appendNode("url", "https://opensource.org/licenses/MIT")

                def developerNode = node.appendNode("developers").appendNode("developer")
                developerNode.appendNode("id", "kale-ko")
                developerNode.appendNode("name", "Kale Ko")
                developerNode.appendNode("url", "https://kaleko.dev/")
                developerNode.appendNode("email", "contact@kaleko.dev")

                def scmNode = node.appendNode("scm")
                scmNode.appendNode("connection", "scm:git:git@github.com:Kale-Ko/GIGHM.git")
                scmNode.appendNode("developerConnection", "scm:git:git@github.com:Kale-Ko/GIGHM.git")
                scmNode.appendNode("tag", "master")
                scmNode.appendNode("url", "https://github.com/Kale-Ko/GIGHM")

                def dependenciesNode = node.appendNode("dependencies")

                def dependencyManagmentNode = node.appendNode("dependencyManagement")
                def dependenciesManagmentNode = dependencyManagmentNode.appendNode("dependencies")

                configurations.runtimeClasspath.resolvedConfiguration.getFirstLevelModuleDependencies().each {
                    if (it.moduleName != "lwjgl-bom") {
                        def dependencyNode = dependenciesNode.appendNode("dependency")

                        if (it.moduleGroup == "org.lwjgl") {
                            dependencyNode.appendNode("groupId", it.moduleGroup)
                            dependencyNode.appendNode("artifactId", it.moduleName)

                            project.lwjgl_natives.split(",").each {
                                def subDependencyNode = dependenciesNode.appendNode("dependency")
                                subDependencyNode.appendNode("groupId", it.moduleGroup)
                                subDependencyNode.appendNode("artifactId", it.moduleName)
                                subDependencyNode.appendNode("classifier", it)
                            }
                        } else {
                            dependencyNode.appendNode("groupId", it.moduleGroup)
                            dependencyNode.appendNode("artifactId", it.moduleName)
                            dependencyNode.appendNode("version", it.moduleVersion)
                        }
                    } else {
                        def dependencyNode = dependenciesManagmentNode.appendNode("dependency")
                        dependencyNode.appendNode("groupId", it.moduleGroup)
                        dependencyNode.appendNode("artifactId", it.moduleName)
                        dependencyNode.appendNode("version", it.moduleVersion)
                        dependencyNode.appendNode("scope", "import")
                        dependencyNode.appendNode("type", "pom")
                    }
                }

                configurations.compileClasspath.resolvedConfiguration.getFirstLevelModuleDependencies().each {
                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", it.moduleGroup)
                    dependencyNode.appendNode("artifactId", it.moduleName)
                    dependencyNode.appendNode("version", it.moduleVersion)
                }
            }
        }
    }
}