plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.toVersion(project.java_version)
targetCompatibility = JavaVersion.toVersion(project.java_version)

archivesBaseName = "gighm"
group = "io.github.kale_ko"
version = project.project_version

configurations {
    depencancy
    depencancy.canBeResolved = true

    implementation {
        extendsFrom depencancy
    }
}

repositories {
    mavenCentral()
}

dependencies {
    depencancy platform("org.lwjgl:lwjgl-bom:${project.lwjgl_version}")
    depencancy "org.lwjgl:lwjgl"
    depencancy "org.lwjgl:lwjgl-opengl"
    depencancy "org.lwjgl:lwjgl-glfw"
    // depencancy "org.lwjgl:lwjgl-openal"

    project.lwjgl_natives.split(",").each {
        depencancy "org.lwjgl:lwjgl::${it}"
        depencancy "org.lwjgl:lwjgl-opengl::${it}"
        depencancy "org.lwjgl:lwjgl-glfw::${it}"
        // depencancy "org.lwjgl:lwjgl-openal::${it}"
    }

    depencancy "org.joml:joml:${project.joml_version}"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes "Name": project.group.replace(".", "/") + "/" + project.archivesBaseName + "/"
    }
}

tasks.register("buildFat", Jar) {
    dependsOn tasks.build

    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    duplicatesStrategy = "exclude"
    classifier = "fat"

    from files(project.sourceSets.main.output)
    from files(project.configurations.depencancy.collect { it.isDirectory() ? it : zipTree(it) })

    manifest {
        attributes "Name": project.group.replace(".", "/") + "/" + project.archivesBaseName + "/",
            "Main-Class": project.group + "." + project.archivesBaseName + ".tests.ExampleScene",
            "Application-Name": project.name
    }
}

tasks.register("buildSources", Jar) {
    dependsOn tasks.build

    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    duplicatesStrategy = "exclude"
    classifier = "sources"

    manifest {
        attributes "Name": project.group.replace(".", "/") + "/" + project.archivesBaseName + "/"
    }

    from project.sourceSets.main.allJava
}

tasks.register("genJavaDocs", Javadoc) {
    dependsOn tasks.build

    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath

    title = project.name + " " + project.version

    mkdir "./build"
    mkdir "./build/docs"
    file("./build/docs/overview.html").text = "<html><body><p class=\"overview\">" + project.project_description + "</p></body></html>"

    options {
        overview "build/docs/overview.html"
        encoding "utf-8"
    }

    exclude "**/tests/*.java"
}

tasks.register("buildJavaDocs", Jar) {
    dependsOn tasks.genJavaDocs

    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    duplicatesStrategy = "exclude"
    classifier = "javadoc"

    manifest {
        attributes "Name": project.group.replace(".", "/") + "/" + project.archivesBaseName + "/"
    }

    from tasks.genJavaDocs
}

tasks.register("buildAll") {
    dependsOn tasks.jar
    dependsOn tasks.buildFat
    dependsOn tasks.buildSources
    dependsOn tasks.buildJavaDocs
}

publishing {
    repositories {
        maven {
            name = "GitHub-Packages"
            url = "https://maven.pkg.github.com/Kale-Ko/GIGHM"

            credentials {
                username = System.getenv("GH_PACKAGES_USER")
                password = System.getenv("GH_PACKAGES_TOKEN")
            }
        }
    }

    publications {
        ghp(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact tasks.jar
            artifact tasks.buildFat
            artifact tasks.buildJavaDocs
            artifact tasks.buildSources

            pom.withXml {
                def node = asNode()

                def packagingNode = node.appendNode("packaging", "jar")

                def licenseNode = node.appendNode("licenses").appendNode("license")
                licenseNode.appendNode("name", "MIT")
                licenseNode.appendNode("url", "https://opensource.org/licenses/MIT")

                def developerNode = node.appendNode("developers").appendNode("developer")
                developerNode.appendNode("id", "kale-ko")
                developerNode.appendNode("name", "Kale Ko")
                developerNode.appendNode("url", "https://kaleko.ga/")
                developerNode.appendNode("email", "contact@kaleko.ga")

                def scmNode = node.appendNode("scm")
                scmNode.appendNode("connection", "scm:git:git@github.com:Kale-Ko/GIGHM.git")
                scmNode.appendNode("developerConnection", "scm:git:git@github.com:Kale-Ko/GIGHM.git")
                scmNode.appendNode("tag", "master")
                scmNode.appendNode("url", "https://github.com/Kale-Ko/GIGHM")

                def dependenciesNode = node.appendNode("dependencies")

                def dependencyManagmentNode = node.appendNode("dependencyManagement")
                def dependenciesManagmentNode = dependencyManagmentNode.appendNode("dependencies")

                configurations.depencancy.resolvedConfiguration.getFirstLevelModuleDependencies().each {
                    def depend = it

                    if (it.moduleName != "lwjgl-bom") {
                        def dependencyNode = dependenciesNode.appendNode("dependency")

                        if (it.moduleGroup == "org.lwjgl") {
                            dependencyNode.appendNode("groupId", it.moduleGroup)
                            dependencyNode.appendNode("artifactId", it.moduleName)

                            project.lwjgl_natives.split(",").each {
                                def subDependencyNode = dependenciesNode.appendNode("dependency")
                                subDependencyNode.appendNode("groupId", depend.moduleGroup)
                                subDependencyNode.appendNode("artifactId", depend.moduleName)
                                subDependencyNode.appendNode("classifier", it)
                            }
                        } else {
                            dependencyNode.appendNode("groupId", it.moduleGroup)
                            dependencyNode.appendNode("artifactId", it.moduleName)
                            dependencyNode.appendNode("version", it.moduleVersion)
                        }
                    } else {
                        def dependencyNode = dependenciesManagmentNode.appendNode("dependency")
                        dependencyNode.appendNode("groupId", depend.moduleGroup)
                        dependencyNode.appendNode("artifactId", depend.moduleName)
                        dependencyNode.appendNode("version", depend.moduleVersion)
                        dependencyNode.appendNode("scope", "import")
                        dependencyNode.appendNode("type", "pom")
                    }
                }
            }
        }
    }
}